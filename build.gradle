buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
}

plugins {
    id 'com.jfrog.bintray' version "1.8.4"
    id 'maven-publish'
    id 'java'
    id 'checkstyle'
}

group 'com.ivelum'
version '0.5.1'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenCentral()
    jcenter()
}


dependencies {
    compile group: 'org.json', name: 'json', version:'20180813'
    compile group: 'com.google.code.gson', name: 'gson', version:'2.8.5'
    testCompile "com.github.tomakehurst:wiremock-jre8:2.22.0"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

checkstyle {
    toolVersion = '8.16'
    configProperties = [ "suppressionFile" : project(':').file('config/checkstyle/suppressions.xml')]
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}


def pomConfig = {
    licenses {
        license {
            name 'MIT'
            url 'https://github.com/praetoriandigital/cub-java/blob/master/LICENSE'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id 'zadoev'
            name 'Roman Zadoev'
            email "roman.zadoev@ivelum.com"
        }
    }

    scm {
        url "https://github.com/praetoriandigital/cub-java"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'com.ivelum'
            artifactId 'cub-java'
            version '0.5.1'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Cub client for java')
                root.appendNode('name', 'cub-java')
                root.appendNode('url', 'https://github.com/praetoriandigital/cub-java')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']

    pkg {
        repo = 'cub-java'
        name = 'cub-java'
        userOrg = 'ivelum'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/praetoriandigital/cub-java.git'
        version {
            name = '0.5.1'
            desc = '0.5.1'
            released  = new Date()
        }
    }

}