plugins {
    id 'maven-publish'
    id 'java'
    id 'checkstyle'
}

group 'com.ivelum'
version '0.16.0'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}


dependencies {
    implementation group: 'org.json', name: 'json', version:'20180813'
    implementation group: 'com.google.code.gson', name: 'gson', version:'2.8.5'
    testImplementation "com.github.tomakehurst:wiremock-jre8:2.22.0"
    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

checkstyle {
    toolVersion = '8.16'
    configProperties = [ "suppressionFile" : project(':').file('config/checkstyle/suppressions.xml')]
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}


def pomConfig = {
    licenses {
        license {
            name 'MIT'
            url 'https://github.com/praetoriandigital/cub-java/blob/master/LICENSE'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id 'zadoev'
            name 'Roman Zadoev'
            email "roman.zadoev@ivelum.com"
        }
        developer {
            id 'sergunich'
            name 'Sergey Anuchin'
            email "sergey.anuchin@ivelum.com"
        }
    }

    scm {
        url "https://github.com/praetoriandigital/cub-java"
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/praetoriandigital/cub-java")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.ivelum'
            artifactId 'cub-java'
            version '0.16.0'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Cub client for java')
                root.appendNode('name', 'cub-java')
                root.appendNode('url', 'https://github.com/praetoriandigital/cub-java')
                root.children().last() + pomConfig
            }
        }
    }
}
